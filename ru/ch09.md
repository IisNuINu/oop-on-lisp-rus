# Создание и инициализация экземпляров

Клиентские программы обычно используют конструкторы для создания экземпляров. Конструкторы вызывают make-instance, который создает, инициализирует и возвращает новый экземпляр. CLOS позволяет вам управлять многими аспектами инициализации, начиная от указания значения по умолчанию для слота и заканчивая настройкой инициализации путем написания метода. Эта глава начинается с описания аргументов make-instance и краткого описания шагов, которые CLOS выполняет при вызове make-instance. Затем в нем показано, как использовать методы для управления инициализацией.

## 9.1 АРГУМЕНТЫ ДЛЯ СОЗДАНИЯ ЭКЗЕМПЛЯРА

Синтаксис make-instance таков

<pre>
make-instance class Srest initargs 
</pre>

Первый аргумент - это класс, который может быть либо именем класса, либо самим объектом класса. Это дает вам представление о том, что make-instance пересекает границу между макроуровнем и функциональным уровнем. Фактически, make-instance - это мощный инструмент, используемый как программистами приложений, так и программистами метаобъектов; мы описываем только те методы, которые предназначены для прикладных программистов.

Аргумент &rest состоит из initargs, что является сокращением от аргументов инициализации. initarg управляет некоторым аспектом инициализации; он может заполнять слот или использоваться методом инициализации, или и тем, и другим.

Каждый initarg состоит из имени инициализирующего параметра, за которым следует значение. Имя инициализации initarg может быть любым символом, не обязательно ключевым словом. Формат аргумента &rest совпадает с форматом аргументов ключевого слова, которые обрабатываются как параметры &key.

## 9.2 КРАТКОЕ ОПИСАНИЕ ТОГО, ЧТО ДЕЛАЕТ MAKE-INSTANCE

Когда вы вызываете make-instance, CLOS выполняет следующие действия:

1. Объединяет initargs, которые вы предоставляете make-instance, со значениями по умолчанию для любых initargs(инициализаций), которые вы явно не предоставляете. Результатом является список инициализации по умолчанию.
2. Гарантирует, что все имена initarg в списке инициализации(initarg) по умолчанию являются допустимыми, и выдает сообщение об ошибке, если это не так. Если :allow-other-keys указано как true при вызове make-instance, все имена initarg являются допустимыми.
3. Выделяет хранилище для экземпляра и создает экземпляр, все слоты которого не связаны.
4. Применяет обобщенную функцию initialize-instance к вновь созданному экземпляру и списку инициализации(initarg) по умолчанию. Основной метод по умолчанию для initialize-instance выполняет следующее:

             a. Инициализирует слоты в соответствии со списком инициализации(initarg) по умолчанию

             b. Инициализирует все слоты, которые имеют :initforms и все еще не связаны

Основной метод для initialize-instance выполняет эту работу, вызывая shared-initialize, который мы обсуждаем в "Процедурном определении: инициализация", стр. 165.

5. Возвращает инициализированный экземпляр.

## 9.3 УПРАВЛЕНИЕ ИНИЦИАЛИЗАЦИЕЙ С ПОМОЩЬЮ ПАРАМЕТРОВ DEFCLASS

В этом примере мы определяем некоторые классы для представления окон(windows). В этих определениях классов используются методы инициализации windows. Окно базового класса предназначено для того, чтобы быть основой всех окон. Форма defclass использует два параметра слота, которые относятся к инициализации — параметры :initarg и :initform.

<pre>
(defclass window () 
  ((x         :initarg :x-position :accessor x-position) 
   (y         :initarg :y-position :accessor y-position) 
   (height    :initarg :height     :accessor window-height) 
   (width     :initarg :width      :accessor window-width) 
   (exposed-p :initform nil        :accessor exposed-p)) 
  (:documentation "Foundation of all windows.")) 
</pre>

### Использование опции слота :initarg

В определении класса window для четырех слотов (x, y, height и width) используется параметр :initarg. Он объявляет четыре символа допустимыми именами инициализации для окна класса. Например, символ : x-position - это имя инициализации. Если вы передадите его make-instance, за которым последует значение, это значение будет сохранено в слоте x. Аналогично, символ :height - это инициализация, которую можно использовать для инициализации слота с именем height.

<pre>
(make-instance 'window :x-position 0 
                       :y-position 0 
                       :height 200 
                       :width 75) 
</pre>

Обратите внимание, что слот с именем exposed-p не использует параметр :initarg. Таким образом, вы не можете инициализировать этот слот, предоставив аргумент make-instance. Слот exposed-p не предназначен для инициализации пользователем.

### Использование опции слота :initform

Слот с именем exposed-p использует параметр слота :initform, чтобы связать начальное значение по умолчанию со слотом. Значение равно nil. Семантика этого слота проста: когда вы впервые создаете окно, оно не отображается. Мы намеренно не предлагаем initarg, потому что мы хотим, чтобы все вновь созданные окна были деэкспонированы(скрыты); такой подход приводит к автоматической инициализации слота в его начальной форме(initform) (которая равна нулю) и эффективно предотвращает инициализацию пользователями слота exposed-p.

### Использование параметра класса :default-initargs

Иногда классу полезно предоставить значение по умолчанию для инициализации. Параметр класса :default-initargs делает это. Он используется в основном для удаленного выполнения по умолчанию; то есть для предоставления значения по умолчанию для унаследованного initarg.

Если initarg(инициализация) предоставляется в вызове make-instance, она переопределяет инициализацию по умолчанию(default initarg). Но если initarg(инициализация) опущена, используется значение инициализации по умолчанию(default initarg).

Мы будем использовать :default-initargs при определении класса, который обычно используется в оконной программе. Полноэкранное окно(full-screen) - это окно, которое занимает весь экран. Высота и ширина такого окна получаются из переменных, которые хранят размеры экрана. Полноэкранное окно обычно располагается в начале координат, поэтому мы также задаем значения по умолчанию для :x-position и :y-position.

<pre>
(defclass full-screen-window (window) () 
  (idefault-initargs 
   :x-position *screen-origin-x* 
   :y-position *screen-origin-y* 
   :height     *screen-height* 
   :width      *screen-width*)) 
</pre>

Этот класс может использоваться отдельно или в качестве строительного блока для других классов. Он имеет те же слоты, что и window, но предлагает значения по умолчанию для четырех инициализаций для удобства клиентов, которым необходимо создавать полноэкранные окна.

### Два вида значений по умолчанию

Важно иметь в виду разницу между :default-initargs и :initform. Параметр :default-initargs задает значение по умолчанию для initarg(инициализации), а параметр :initform задает значение по умолчанию для слота.

Если вы намерены разрешить пользователям инициализировать слот, то вам следует

* Использовать :initarg для объявления символа для инициализации слота
* Использовать :default-initargs, если вы хотите присвоить этому initarg значение по умолчанию

Если вы не собираетесь разрешать пользователям инициализировать слот, то вам следует

* Не использовать параметр :initarg
* Используйте :initform, если вы хотите присвоить слоту начальное значение по умолчанию

Эти два варианта вступают в конфликт, если они используются вместе. Рассмотрим, что происходит, когда слот имеет значение по умолчанию через :initform и инициализацию через :initarg, которая сама имеет значение по умолчанию через :default-initargs. Значение по умолчанию, заданное в :default-initargs, эффективно переопределяет значение по умолчанию, заданное в :initform.

Для обоих этих параметров форма значения по умолчанию вычисляется каждый раз, когда она используется. Значение :initform вычисляется каждый раз, когда оно используется для инициализации слота. Значение initarg в :default-initargs вычисляется при каждом вызове make-instance, и этот initarg не передается в качестве аргумента make-instance.

## 9.4 УПРАВЛЕНИЕ ИНИЦИАЛИЗАЦИЕЙ С ПОМОЩЬЮ МЕТОДОВ

Когда вызывается make-instance, он создает экземпляр и вызывает обобщенную функцию initialize-instance для инициализации нового экземпляра. CLOS предоставляет основной метод по умолчанию для initialize-instance, который заполняет слоты значениями в соответствии с их initargs и initforms. Вы можете настроить инициализацию экземпляров, написав метод для initialize-instance для выполнения дополнительной работы.

### Определение After(после)-методов для initialize-instance(инициализации экземпляра)

Оконная система, вероятно, должна отслеживать все окна. Здесь мы добавляем новое окно в структуру данных, которая отслеживает деэкспонированные(deexposed/скрытые) окна.

<pre>
(defmethod initialize-instance :after ((w window) Skey) 
   (push w *deexposed-windows*)) 
</pre>

Обычно вы должны определять after-methods для initialize-instance (как мы делаем здесь) вместо primary methods. Основной метод переопределил бы основной метод по умолчанию и предотвратил бы обычную инициализацию слота.

Поскольку методы для initialize-instance получают все значения initargs по умолчанию в качестве аргументов, методы для initialize-instance должны использовать &key в своих лямбда-списках. Результатом использования &key здесь является то, что метод допускает ключевые слова без указания того, что он использует какие-либо аргументы ключевого слова. Для получения более подробной информации о лямбда-списках метода см. "Конгруэнтные лямбда-списки", стр. 132.

### Метод по умолчанию для initialize-instance(инициализации экземпляра)

make-instance вызывает initialize-instance с экземпляром и списком инициализации по умолчанию(defaulted initarg). С этими аргументами метод по умолчанию для initialize-instance заполняет ячейки значениями следующим образом:

	      > Шаг 1 Если вы предоставляете заполняющий слот initarg для make-instance, то его значение 
	      >           сохраняется в соответствующем слоте. (Параметр initarg, заполняющий слот, задается
 	      >           параметром слота :initarg.)
	      > Шаг 2 Если слот не заполнен на шаге 1, и у initarg есть форма значения по умолчанию, то эта форма
 	      >          вычисляется, и результат сохраняется в слоте. (Значение по умолчанию для initarg задается
	      >          параметром класса :default-initargs.)
	      > Шаг 3 Если слот не заполнен на шаге 2, и слот имеет форму начального значения по умолчанию, то 
	      >          эта форма вычисляется, и результат сохраняется в слоте. (Значение по умолчанию для слота
	      >          задается параметром слота :initform.)
	      > Шаг 4 Если слот не заполнен на шаге 3, то слот остается несвязанным.

Метод по умолчанию для initialize-instance выполняет эту инициализацию путем вызова shared-initialize, обобщенной функции, которая вызывается в других контекстах, а также при создании новых экземпляров. Мы подробно описываем общую инициализацию в разделе "Изоляция работы, разделяемой между процедурами", стр. 167.

## 9.5 АРГУМЕНТЫ ИНИЦИАЛИЗАЦИИ

В этом разделе более подробно описываются инициализации, уделяя особое внимание тому, как они используются для инициализации новых экземпляров. Инициализации используются и в других контекстах; см. "Выполнение инициализации с помощью Initargs", стр. 168.

### Действительность имен Initarg

Имя initarg должно быть объявлено как допустимое для данного класса, прежде чем оно будет использовано в вызове make-instance. Существует два способа объявить, что имя initarg является допустимым:

	     > :initarg slot option 
	     >            Объявляет символ как допустимое имя initarg(инициализации) и указывает, что значение initarg
	     >            должно быть сохранено в слоте.
	     >            Такой символ называется заполняющим слот initarg. Параметр слота :initarg наследуется
	     >            объединением: все имена initarg, объявленные классом или любым из его суперклассов,
	     >            действительны для класса.

	       > initialize-instance methods 
	       >            Объявляет все имена параметров &key в лямбда-списке как допустимые имена инициализации
	       >            для класса. (Методы для других обобщенных функций также объявляют параметры &key как
	       >            допустимые имена initarg. Смотрите "Объявление имен Initarg как допустимых", стр. 170.)

Кроме того, имя инициализации :allow-other-keys допустимо для всех классов. Его значение по умолчанию равно nil, что означает, что CLOS проверяет действительность всех инициализаций и выдает сообщение об ошибке, если обнаружено недопустимое имя инициализации. Если вы вызываете make-instance и даете :allow-other-keys, за которым следует значение, отличное от нуля, эта проверка ошибок отключена.

Если лямбда-список метода инициализации использует :allow-other-keys, все возможные символы объявляются как допустимые инициализации. Другими словами, использование :allow-other-keys в методе инициализации отключает проверку имен initarg на ошибки.

### Наследование Initargs(инициализаций) по умолчанию

Параметр класса :default-initargs связывает значение по умолчанию с initarg. Набор initargs(инициализаций) по умолчанию наследуется объединением, но значение по умолчанию любого одного параметра initarg наследуется затенением. Другими словами,

	     > Union(Объединение)         Набор initargs класса по умолчанию - это объединение всех
	     >                                         initargs(инициализаций) по умолчанию, предоставляемых классами в его
	     >                                         списке предшествования классов

	       > Shadowing(Затенение)            значения по умолчанию для любого одного initarg происходит от
	       >                                               наиболее конкретного класса, который предоставил для него значение по
	       >                                               умолчанию

### Разделение Initarg(инициализаций) и имен слотов

Когда вы используете опцию слота :initarg, имя initarg(инициализации) не зависит от имени слота. Эта независимость обеспечивает определенный уровень абстракции; клиенты не могут предполагать, что инициализация отображается непосредственно в слот с тем же именем. Фактически, некоторые initargs(инициализации) могут вообще не отображаться в слот, а некоторые слоты могут быть заполнены значениями, которые вычисляются на основе нескольких initargs(инициализаций).

Например, класс triangle может принимать три initargs, по одной для длины каждой стороны. Однако класс может быть реализован для хранения длины двух сторон и угла между ними, который может быть вычислен из трех initargs. Таким образом, незаметно для клиента третья сторона использовалась бы для расчета противоположного угла, а затем была бы отброшена.

В следующем примере используется этот подход. Обратите внимание, что метод инициализации объявляет :side-a, :side-b и :side-c как допустимые имена initarg(инициализации), тогда как форма defclass не объявляет никаких имен initarg(инициализации). Мы определяем конструктор make-triangle, чтобы создать абстрактный интерфейс для создания треугольников и сделать все стороны обязательными аргументами. Вся работа по инициализации выполняется в методе для initialize-instance.

<pre>
(defclass triangle (shape) 
  ((a : reader side-a) 
   (b :reader side-b) 
   (angle-C :reader angle-C)))

;;; Do all initialization in this method 
(defmethod initialize-instance .after 
    ((tri triangle) skey side-a side-b side-c) 
   (let* ((float-a (coerce a 'float)) 
          (float-b (coerce b 'float)) 
          (float-c (coerce c 'float)) 
          (float-angle-C (three-sides-to-angle 
                          float-c float-a float-b))) 
      (with-slots (a b angle-C) tri 
         (setf a float-a) 
         (setf b float-b) 
         (setf angle-C float-angle-C)))) 

;;; Define the constructor 
(defun make-triangle (side-a side-b side-c) 
   (make-instance 'triangle :side-a side-a 
                            :side-b side-b 
                            :side-c side-c)) 
</pre>

##9.6 КОНСТРУКТОРЫ

Мы рекомендуем использовать конструкторы в качестве внешнего интерфейса для создания экземпляров, поскольку конструкторы добавляют ценный уровень абстракции между клиентом и реализацией. Рассмотрим треугольники: название конструктора, make-triangle, подразумевает "создание треугольника", что является концепцией более высокого уровня, чем "создание экземпляра класса triangle".

Еще одним преимуществом конструкторов является то, что они могут использовать всю мощь обработки аргументов Common Lisp. Синтаксис make-instance крайне ограничен: после первого аргумента (класса) следует параметр &rest, состоящий из initargs. Во многих случаях семантику класса можно лучше выразить с помощью обязательных аргументов, необязательных аргументов и так далее. Например, в случае с треугольниками аргумент &rest для make-instance не подразумевает, что для создания треугольника требуются все три initargs(инициализации) — стороны. Конструктор, однако, может сделать три стороны обязательными аргументами; синтаксис конструктора точно отражает семантику треугольников.

Возможно, самое важное, конструкторы скрывают реализацию объектов, что позволяет вам изменять реализацию, не нарушая работу клиентских программ. Если вы рекламируете(предоставляете как внешний интерфейс) конструкторы как внешний интерфейс, вы можете позже перейти к представлению объекта defstruct или изменить имя или initargs(инициализации) класса, не делая недействительными клиентские программы. Конструкторы также могут выбрать один из нескольких классов на основе его аргументов. Если вы рекламируете make-instance как внешний интерфейс, вы не сможете внести эти изменения в рамках реализации.