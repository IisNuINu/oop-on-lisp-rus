# Программирование с помощью методов

Программа блокировки проиллюстрировала основные функции CLOS. В этой главе описываются дополнительные полезные методы, в том числе как написать методы для выполнения следующих действий:

* Представление информации без сохранения ее в слотах
* Добавление поведения к обобщенной функции средства доступа(accessor)
* Специализироваться на более чем одном аргументе
* Специализироваться на типах Common Lisp, таких как строки и массивы
* Специализироваться на отдельном объекте Lisp

В этой главе мы используем три примера. Первый пример представляет треугольники как экземпляры классов и демонстрирует различные способы хранения информации, связанной с треугольниками. Второй пример представляет собой программу установки, предназначенную для установки различных программных продуктов в разных операционных системах; показано, как писать методы, которые специализируются на более чем одном аргументе. Третий пример - это программа, поддерживающая удаленную оценку/выполнение, которая иллюстрирует методы, специализирующиеся на типах Common Lisp и на отдельных объектах Lisp.

## 4.1 ВАРИАНТЫ РЕАЛИЗАЦИИ: МЕТОДЫ ПРОТИВ СЛОТОВ

CLOS поддерживает несколько способов представления определенных видов информации, связанных с экземпляром или классом. В этом разделе вводится концепция общего(для всех экземпляров класса) слота и обсуждаются плюсы и минусы представления информации в слотах по сравнению с методами.

### Локальные и общие(Shared) слоты

Существует два вида слотов. Локальный слот хранит информацию о состоянии конкретного экземпляра. Каждый экземпляр поддерживает свою индивидуальную копию слота со своим собственным значением. Это тип слота по умолчанию. Все слоты, упомянутые ранее в этой книге, являются локальными.

Общий слот хранит информацию о состоянии класса в целом. Существует только одно значение общего слота; оно связано с классом и совместно используется всеми экземплярами класса. Если один экземпляр изменяет значение общего слота, новое значение будет видно всем экземплярам, если они прочитают слот.

Например, мы могли бы определить класс с именем triangle. Каждый экземпляр хранит длину своих трех сторон в локальных слотах с именами side-a, side-b и side-c. Это должны быть локальные слоты, чтобы каждый экземпляр мог отслеживать свои собственные измерения.

Предположим, класс triangle является одним из нескольких классов в программе, которая обрабатывает геометрические фигуры. Протокол shape(фигуры) гласит, что обобщенная функция number-of-sides возвращает количество сторон заданной фигуры. Для класса triangle мы можем сохранить эту информацию в общем слоте с именем number-of-sides и предоставить для него метод считывания. Общий слот подходит, поскольку все экземпляры класса triangle имеют одинаковое количество сторон.

![Рисунок 4.1: Локальные и общие слоты.](images/f04-01.jpg )**Рисунок 4.1: Локальные и общие слоты.**

На рисунке 4.1 показано, что каждый экземпляр хранит свои индивидуальные значения для локальных слотов side-a, side-b и side-c, но существует только одно значение для общего количества сторон слота. Локальные слоты хранятся внутри каждого экземпляра, а общие слоты хранятся внутри самого класса.

Форма defclass определяет слоты и управляет тем, является ли слот локальным или общим. Параметр слота :allocation  указывает, является ли слот локальным или общим. Значение по умолчанию равно :allocation :instance(экземпляр), что означает, что слот является локальным. Чтобы указать, что слот должен быть общим, вы указываете параметр :allocation :class.

Следующая форма defclass определяет три локальных слота и один общий слот для класса triangle:

<pre>
(defclass triangle (shape) 
  ((side-a :accessor side-a
           :initarg :side-a) 
   (side-b :accessor side-b
           :initarg :side-b) 
   (side-c :accessor side-c
           :initarg :side-c) 
   (number-of-sides :reader number-of-sides 
                    :initform 3 
                    :allocation :class))) 
</pre>

Считыватель number-of-sides может быть использован для любого экземпляра triangle(треугольника). Метод reader возвращает значение слота с именем number-of-sides, которое является общим для всех экземпляров класса triangle.

### Полезные функции Triangle(Треугольника)
В следующих примерах мы обозначаем каждый угол треугольника в соответствии с его противоположной стороной. Angle-A(Угол-A) является противоположной side-a(стороной-a) и примыкает к side-b(стороне-b) и side-c(стороне-c). На рисунке 4.2 показано, как углы и стороны связаны друг с другом. Рисунок 4.2 Углы и стороны треугольника. Здесь мы определяем две полезные функции для работы с треугольниками, которые мы используем в следующих примерах:

![Рисунок 4.2: Углы и стороны треугольника.](images/f04-02.jpg )**Рисунок 4.2: Углы и стороны треугольника.**

<pre>
;;; Return the area of a triangle, given three sides. 
;;; Algorithm is: area - ab(sin C)/2 
(defun area-of-triangle (a b c) 
   (let ((angle-C (three-sides-to-angle c a b))) 
      (* a b (sin angle-C) .5)))

;;; Return the angle A between adjacent sides b and c 
;;; and opposite side a, given all sides of a triangle 
;;; Law of Cosines: aA2 - bA2 + cA2 - 2bc(cos A) 
(defun three-sides-to-angle (a b c) 
   (acos (/ (- (+ (expt b 2) (expt c 2)) 
               (expt a 2)) 
            (* 2 b c)))) 
</pre>

Мы подробно остановимся на примере треугольника в разделе "Пример переопределения элементов CLOS", стр. 144.

### Представление информации Об Экземпляре

В программе shape нам может потребоваться определить площадь заданной фигуры(area). Существует два способа представления площади фигуры: вычислить ее с помощью метода или сохранить площадь в локальном слоте. Например, вот способы представления площади треугольника:

	 > Метод                     Определите метод, который вычисляет площадь на основе
	 >                               длин сторон треугольника.

<pre>
(defmethod area ((tri triangle)) 
   (area-of-triangle (side-a tri) 
                     (side-b tri) 
                     (side-c tri))) 
</pre>
	>Локальный слот             Определите локальный слот с именем area, в котором
	>                                      хранится площадь каждого треугольника и метод считывания
	>                                      для этого слота.

<pre>
(defclass triangle (shape) 
  ((side-a :accessor side-a :initarg :side-a) 
   (side-b :accessor side-b :initarg :side-b) 
   (side-c :accessor side-c :initarg :side-c) 
   (number-of-sides :reader number-of-sides 
                    :initform 3 
                    :allocation :class) 
   (area :reader area :initarg :area))) 
</pre>

	>                          Мы должны вычислить площадь при первом создании
	>                          треугольника. Один из эффективных способов сделать это
	>                          - определить конструктор с именем make-triangle, который
	>                          инициализирует area:

<pre>
(defun make-triangle (side-a side-b side-c) 
   (make-instance 'triangle 
                  :side-a side-a 
                  :side-b side-b 
                  :side-c side-c 
                  :area (area-of-triangle 
                         side-a side-b side-c))) 
</pre>

	>                          Поскольку многим программам необходимо выполнять
	>                          инициализацию, подобную этой, CLOS предоставляет технику,
	>                          специально предназначенную для этой цели, называемую
	>                          методами инициализации; мы обсуждаем их в разделе
	>                          "Управление инициализацией с помощью методов", стр. 159.

	>                          Также необходимо пересчитывать площадь всякий раз, когда
	>                          изменяется какая-либо из длин сторон, что мы демонстрируем
	>                          в разделе "Определение вспомогательных методов для средств
	>                          доступа(Accessors)", стр. 71.

Независимо от того, какой выбор реализации вы сделаете, клиент получает area одним и тем же способом: вызывая обобщенную функцию area. Выбор внутреннего представления остается за программистом. Эффект от использования метода для вычисления площади каждый раз таков: требуется меньше места для хранения, а создание объекта и обновление длины стороны выполняются быстрее, но запрос площади(area) выполняется медленнее. Эффекты хранения площади(area) в слоте таковы: запрос  area выполняется быстрее, но может потребоваться больше места для хранения, а создание объекта и обновление длины стороны выполняются медленнее.

### Представление информации О Классе

Мы уже упоминали о необходимости представления информации, связанной с классом, такой как количество сторон треугольников. CLOS предлагает два способа сделать это:

	> Метод                Определите метод, который возвращает информацию.
	>                           Количество сторон "хранится" только в теле метода, а не
	>                           в слоте.

<pre>
(defmethod number-of-sides ((tri triangle))
   3) 
</pre>

	> Общий слот                Определите общий слот и метод считывания для обобщенной
	>                                   функции number-of-sides. Две формы defclass для triangle,
	>                                   приведенные ранее, показывают, как определить общий слот
	>                                   (используйте параметр слота :allocation :class) и метод
	>                                   считывания (укажите параметр слота :reader number-of-sides
	>                                   с числом сторон считывателя).

Здесь компромисс заключается между скоростью запроса и удобством обновления информации, связанной с классом. Когда метод хранит информацию, запрос, вероятно, выполняется быстрее, поскольку доступ к слоту не требуется. Этот метод также может сэкономить место для хранения, поскольку информация хранится одним способом, а не в слоте и способе считывания. Однако единственный способ изменить информацию - это переопределить метод. Этот подход работает, но он требует программирования (нахождения определения метода, его редактирования и компиляции), а не просто использования программы (вызова обобщенной функции writer). В некоторых приложениях вы можете разумно предположить, что информация, связанная с классом, никогда не изменится, поэтому обновление не является проблемой. Например, мы знаем, что количество сторон треугольника всегда будет равно 3, и нет необходимости когда-либо обновлять эту информацию. Однако в других контекстах может потребоваться изменить информацию, связанную с классом.

Когда информация хранится в общем слоте, вы можете удобно изменить ее, используя обобщенную функцию записи. Однако (в зависимости от реализации CLOS) запрос может выполняться медленнее и может использоваться больше места для хранения.

## 4.2 ПРОГРАММИРОВАНИЕ С ПОМОЩЬЮ СРЕДСТВ ДОСТУПА(ACCESSORS)

В этом разделе описывается, как изменить поведение обобщенной функции доступа, и обсуждаются различные способы доступа к слотам.

### Автоматически Сгенерированные Средства доступа(Accessors)

Предоставляя defclass параметр слота :accessor, вы просите CLOS сгенерировать метод для обобщенной функции reader и метод для обобщенной функции writer. Метод reader просто возвращает значение слота, а метод writer записывает новое значение в слот.

Это первичные методы, которые ведут себя так же, как и любой другой первичный метод, что означает, что вы можете написать вспомогательные методы для выполнения дополнительных вычислений. То есть вы можете определить методы before и after, чтобы улучшить поведение первичных методов для обобщенных функций reader и writer.

### Определение вспомогательных методов для средств доступа(Accessors)

Здесь мы продолжаем использовать пример с треугольником(triangle). Напомним, что определение класса triangle предоставляет опцию слота :accessor для генерации методов для устройства чтения с именем side-a и соответствующего устройства записи с именем (setf side-a).

Ранее мы упоминали о возможности сохранения площади треугольника в слоте и пересчета площади каждый раз, когда изменяется длина стороны треугольника. Чтобы использовать этот подход, нам нужен метод, который будет выполняться каждый раз, когда новое значение сохраняется в любом из слотов side-a, side-b или side-c. Мы можем сделать это, определив after-методы для обобщенной функций writer (setf side-a), (setf side-b) и (setf side-c).

Вы можете использовать defmethod для написания вспомогательного метода для обобщенной функции reader или обобщенной функции writer. Первый аргумент - это имя обобщенной функции, которое для читателя является символом, а для записи - списком, таким как (setf symbol). В методах для обобщенной функций записи первый параметр в лямбда-списке является параметром для нового значения, которое будет записано в слот. Например:

<pre>
(defmethod (setf side-a) :after (new-length (tri triangle)) 
   (setf (area tri) 
         (area-of-triangle new-length 
                           (side-b tri) 
                           (side-c tri)))) 
</pre>

Это after-метод, который выполняется после основного метода для (setf side-a). Этот метод вычисляет площадь треугольника и сохраняет ее в слоте area каждый раз, когда вызывается функция записи (setf side-a). Нам нужно было бы написать аналогичные  after- методы для (setf side-b) и (setf side-c), чтобы гарантировать, что area обновляется всякий раз, когда изменяется какая-либо из длин сторон.

## Определение основных методов для средств доступа(Accessors)

Опции слотов :accessor, :reader и :writer являются удобными функциями CLOS. Вы могли бы реализовать основные методы для чтения и записи слота самостоятельно, используя defmethod. Тело метода чтения будет использовать примитивное значение слота для считывания значения слота, где в качестве метода записи будет использоваться setf со значением слота.

Например, в нашем предыдущем определении для класса triangle мы дали параметр :accessor для определения reader и writer для side-a. В качестве альтернативы, мы могли бы вручную определить основные методы для читателей и авторов.

<pre>
;;; Defining the reader side-a 
(defmethod side-a ((tri triangle)) 
   (slot-value tri 'side-a)) 

;;; Defining the writer (setf side-a) 
(defmethod (setf side-a) (new-side-a (tri triangle)) 
   (setf (slot-value tri 'side-a) new-side-a))
</pre>

Обычно нет никакого преимущества в определении основного метода средства доступа вручную, потому что почти во всех случаях вы хотите, чтобы основной метод просто считывал или записывал значение слота. Когда вам нужно сделать что-то особенное, вы можете предоставить метод "до"(before-) или "после"(after-) для работы в сочетании с основным методом, вместо предоставления специального основного метода.

### Средства доступа(Accessors) против slot-value

Реализация первичных методов для средств доступа(accessors) (независимо от того, генерируется ли метод автоматически CLOS или определяется вручную) выполняется в терминах slot-value(значения слота). Вы можете прочитать значение любого слота с помощью slot-value, и вы можете записать значение слота, используя setf с помощью slot-value. Важно понимать разницу между использованием slot-value(значения слота) и использованием средства доступа(accessor).

Ожидается, что клиенты программы будут использовать средства доступа(accessors) для чтения и записи слотов, поскольку средства доступа обычно являются рекламируемым(объявляемым) интерфейсом. Напротив, slot-value является базовой реализацией средств доступа и не предназначен для использования в других контекстах. Чтобы вызвать slot-value, вам необходимо знать название слота, которое является строго внутренней деталью программы. Рассмотрим, что произойдет, если разработчик изменит внутреннее представление этой информации, изменив имя слота или сохранив информацию в методе вместо слота: предположительно, разработчик определяет новые методы для обобщенных функций, чтобы заменить методы доступа, поэтому вызывающие объявленные обобщенные функции продолжают работать. Однако любые вызывающие slot-value для этого слота будут признаны недействительными.

slot-value обращается к слоту напрямую, без вызова каких-либо методов доступа. Это является недостатком, если программа зависит от методов доступа для выполнения необходимой работы, связанной с доступом к слоту. Напомним, что в примере треугольника используются after-методы для записи слотов длины стороны для обновления области; если вы используете setf из slot-value для записи значения длины стороны, значение слота области будет неверным, поскольку методы доступа не будут вызваны.

Вы должны использовать slot-value для двух целей: если вы хотите определить средство доступа вручную или если вы хотите получить доступ к слоту без вызова каких-либо методов доступа. Последнее использование slot-value может быть полезно при отладке программы.

### Использование with-accessors и with-slots

Другой способ получить доступ к слотам - использовать with-accessors или with-slots, два макроса, которые позволяют вам получать доступ к слотам с помощью имен переменных. В теле with-accessors использование переменной имеет тот же эффект, что и вызов соответствующей обобщенной функции accessor. В отличие от этого, with-slots переводит использование переменной в вызов slot-value, поэтому методы доступа не вызываются.

Цель этих макросов - упростить части кода, которые часто обращаются к слотам, за счет поддержки синтаксиса быстрого доступа. Таким образом, использование with-accessors - это ярлык для вызова средств доступа, а использование with-slots - это ярлык для вызова slot-value. Использование этих макросов может привести к более сжатому коду, особенно когда тела методов часто используют либо средства доступа, либо значение слота.

В примере с треугольником нам может понадобиться метод для возврата угла A, который противоположен стороне a и примыкает к сторонам b и c. Здесь мы показываем два эквивалентных способа доступа к сторонам.

Вызываем читателей чтобы получить сторону:

<pre>
(defmethod angle-A ((tri triangle)) 
   (three-sides-to-angle (side-a tri) 
                         (side-b tri) 
                         (side-c tri))) 
</pre>

Используем with-accessors, чтобы получить стороны:

<pre>
(defmethod angle-A ((tri triangle)) 
   (with-accessors ((a side-a) 
                    (b side-b) 
                    (c side-c)) 
       tri 
      (three-sides-to-angle a b c))) 
</pre>

Вы также можете записать значение слота, используя setq или setf с переменной. В следующей таблице показано, как переменные могут быть использованы в теле формы with-accessors в показанном методе:

| This Form         | Translates to This Form  |
|-------------------|--------------------------|
|    a              | (side-a tri)             |
| (setq a value)    | (setf (side-a tri) value)|
| (setf a value)    | (setf (side-a tri) value)|

Макрос with-accessors требует, чтобы вы указали переменную (например, a) для каждого аксессуара (например, side-a). Хотя вы можете указать, что переменная должна иметь тот же символ, что и средство доступа, для этого не существует краткого синтаксиса; вам всегда нужно перечислять имена как переменной, так и средства доступа.

Макрос with-slots имеет краткий синтаксис: вы перечисляете слоты, к которым хотите получить доступ, а затем можете получить к ним доступ по их именам. Здесь мы показываем два эквивалентных способа прямого доступа к слотам — вызов slot-value и использование with-slots:

<pre>
;;; Using slot-value to access the slots 
(defmethod angle-A ((tri triangle)) 
   (three-sides-to-angle (slot-value tri 'side-a) 
                         (slot-value tri 'side-b) 
                         (slot-value tri 'side-c))) 

;;; Using with-slots to access the slots 
(defmethod angle-A ((tri triangle)) 
   (with-slots (side-a side-b side-c) 
       tri 
      (three-sides-to-angle side-a side-b side-c))) 
</pre>

В следующей таблице показано, как переменные могут быть использованы в теле формы with-slots в показанном методе:

| Эта форма      | Переводится в эту форму             
|----------------|--------------------------------------|
|   a            | (slot-value tri 'side-a)             |
| (setq a value) | (setf (slot-value tri 'side-a) value)|
| (setf a value) | (setf (slot-value tri 'side-a) value)|

Обратите внимание, что при использовании with-slots или with-accessors форма экземпляра оценивается только один раз.

### Донастройка поведения несвязанных слотов

Если слот не инициализирован, и никакое значение не было сохранено в нем с помощью записи, слот не привязан. Если вы попытаетесь прочитать значение несвязанного слота, будет выдан сигнал об ошибке.

Поведение по умолчанию (сигнализация об ошибке) поддерживается обобщенной функцией slot-unbound. То есть, если вы пытаетесь прочитать несвязанный слот, CLOS вызывает обработчик исключений slot-unbound. Обобщенная функция slot-unbound имеет системный метод по умолчанию, который сигнализирует об ошибке; вы можете специализировать эту обобщенную функцию, если хотите изменить то, что происходит, когда несвязанные слоты считываются в экземплярах определенного класса.

## 4.3 МУЛЬТИМЕТОДЫ

Многие объектно-ориентированные программы могут быть написаны с использованием методов, которые специализируются только на одном параметре. Однако иногда полезно писать методы, которые специализируются более чем на одном параметре. Они называются мультиметодами.

Техника использования мультиметодов предназначена для операций, реализация которых действительно зависит от типа более чем одного аргумента.

Мы вводим несколько методов, обсуждая сценарий инициализации. Предположим, компания продает различные программные продукты, каждый из которых может работать на различных операционных системах. Процедура установки зависит от типа программного продукта и от типа операционной системы. Эта компания хочет предоставить универсальную программу установки для автоматизации процесса установки.

В настоящее время компания поддерживает два программных продукта, Life и Adventure, в двух операционных системах, Genera и Unix. Компания планирует поддерживать дополнительные программные продукты и операционные системы в будущем, поэтому программа установки должна быть расширяемой. В этом примере мы используем следующее представление программных продуктов и операционных систем:

* Программные продукты представлены классами life и adventure
* Все программные продукты построены на классе basic-product
* Операционные системы представлены классами genera и unix
* Все операционные системы построены на классе basic-os

### Проверка аргументов с помощью мультиметодов

Мы хотим предоставить одну функцию верхнего уровня для установки любого из наших поддерживаемых продуктов в любую из поддерживаемых нами операционных систем. Мы можем сделать это с помощью обобщенной функции install, которая ожидает программный продукт в качестве первого аргумента и операционную систему в качестве второго аргумента:

<pre>
(defgeneric install (software-product operating-system) 
   (:documentation "Installs software on the operating system.")) 
</pre>

Метод 1 применим, когда оба аргумента допустимы; то есть первый аргумент имеет тип basic-product, а второй аргумент имеет тип basic-os:

<pre>
;;; Method 1 
(defmethod install ((sw basic-product) (os basic-os)) 
   body) 
</pre>

Метод 1 является мультиметодом; он специализируется на двух параметрах. Этот метод применим только в том случае, если оба аргумента допустимы. Тело этого метода выполнит инсталяцию. (Пока мы оставляем тело метода пустым.)

Что делать, если пользователь вызывает install с недопустимыми аргументами? То есть, что, если первый аргумент не является программным продуктом, или второй аргумент не является операционной системой, или и то, и другое? CLOS будет искать применимые методы, не найдет ни одного и выдаст сообщение об ошибке типа "Нет применимого метода".

Такое сообщение об ошибке не дает понять, была ли ошибка вызвана ошибкой в программе или неправильным типом ввода, введенного пользователем. Для средства установки мы должны предоставить пользователю более информативное сообщение об ошибке. Мы можем предложить три метода для выполнения этой работы.

<pre>
;;; Method 2 
(defmethod install ((sw basic-product) non-os) 
   (error "Cannot install because ~A is not 
a recognized operating system." non-os)) 

;;; Method 3 
(defmethod install (non-product (os basic-os)) 
   (error "Cannot install because ~A is not a 
recognized software product." non-product)) 

;;; Method 4 
(defmethod install (non-product non-os) 
   (error "Cannot install because ~A is not a 
recognized software product and -A is not 
a recognized operating system." 
non-product non-os)) 
</pre>

Мы предполагаем, что для любой пары аргументов вызывается только один метод. Если оба аргумента допустимы, для выполнения установки следует вызвать метод 1. В противном случае следует вызвать наиболее подходящий метод из методов 2, 3 и 4, чтобы выдать информативное сообщение.

Методы правильно написаны для выполнения их предполагаемых целей. Поскольку все они являются первичными методами, мы знаем, что вызывается только наиболее конкретный применимый метод. CLOSE выбирает этот метод, сначала находя набор применимых методов, а затем ранжируя эти методы в порядке приоритета. В следующих двух разделах обсуждается применимость и порядок приоритета мультиметодов.

### Применимость Мульти-методов

CLOS позволяет методам для одной и той же обобщенной функции специализировать любой из требуемых параметров. Методы установки используют преимущества этой гибкости:

| Method     |  Lambda-list                      |
|------------|-----------------------------------|
| Method 1   | ((sw basic-product) (os basic-os))|
| Method 2   | ((sw basic-product) non-os)       |
| Method 3   | (non-product (os basic-os))       |
| Method 4   | (non-product non-os)              |

Когда вызывается install, CLOS ищет применимые методы. Метод применим, если каждому специализированному параметру удовлетворяет соответствующий аргумент обобщенной функции. "Удовлетворено" означает, что аргумент имеет тип специализатора параметра; это экземпляр самого класса или экземпляр подкласса.

Неспециализированный параметр эквивалентен классу t, являющемуся специализатором параметра. Поскольку все объекты имеют тип t, аргумент всегда удовлетворяет неспециализированному параметру. Таким образом, метод 4 применим для любых двух аргументов, независимо от их типов.

Предположим, что install вызывается с двумя допустимыми аргументами. Здесь `*life*` - это экземпляр life, а `*general*` - это экземпляр general.

<pre>
;;; Here install is called with two valid arguments, 
(install *life* *genera*) 
</pre>

Оба метода 1 и 2 применимы, как видно из следующих тестов:

<pre>
;;; Method 1 is applicable because these forms are true. 
(typep *life* 'basic-product) 
(typep *genera* 'basic-os) 
;;; Method 2 is applicable because these forms are true. 
(typep *life* 'basic-product) 
(typep *genera* 't) 
</pre>

Аналогичным образом, применимы методы 3 и 4:

<pre>
;;; Method 3 is applicable because these forms are true. 
(typep *life* 't) 
(typep *genera* 'basic-os) 
;;; Method 4 is applicable because these forms are true. 
(typep *life* 't) 
(typep *genera* 't) 
</pre>

Ранее мы заявляли, что метод применим, если каждому специализированному параметру удовлетворяет соответствующий аргумент обобщенной функции.
Это правило справедливо и для мультиметодов. Мы можем уточнить это утверждение, отметив, что все требуемые параметры должны быть удовлетворены, и что любые неспециализированные требуемые параметры обрабатываются так, как если бы они имели класс с именем t в качестве специализаторов параметров.

| Правило применимости нескольких методов:                              |
|-----------------------------------------------------------------------|
| Метод применим, если каждому из его требуемых параметров удовлетворяет|
| соответствующий аргумент обобщенной  функции.                         |
| Неспециализированный обязательный параметр эквивалентен параметру,    |
| который специализируется на классе с именем t.                        |

Когда CLOS находит более одного применимого основного метода, вызывается только самый конкретный из них. Таким образом, CLOS должны ранжировать применимые методы в порядке приоритета. Мы продолжаем этот пример и ранжируем методы с 1 по 4 в порядке приоритета.

### Порядок приоритета нескольких методов

CLOS ранжирует два применимых метода в порядке приоритета, рассматривая требуемые аргументы метода слева направо по отношению к их спецификаторам параметров. Математики называют это лексико-графическим порядком.

Здесь мы показываем класс специализатора параметров первого требуемого параметра для четырех методов:

| Метод    |Первый параметр специализирован на|
|----------|----------------------------------|
| Method 1 | basic-product                    |
| Method 2 | basic-product                    |
| Method 3 | t                                |
| Method 4 | t                                |

Список приоритетов классов соответствующего аргумента обобщенной функции определяет, какой из этих классов является более специфичным. Класс первого аргумента, *life*, - это life, а список приоритетов его класса -

<pre>
(life basic-product t) 
</pre>

Поскольку basic-product(базовый продукт) более специфичен, чем t, методы 1 и 2 более специфичны, чем методы 3 и 4. Чтобы ранжировать приоритет метода 1 по сравнению с методом 2, CLOS переходит к следующему требуемому параметру и сравнивает пару специфицирующих параметров:

| Метод    | Второй параметр специализирован на  |
|----------|-------------------------------------|
| Method 1 | basic-os                            |
| Method 2 | t                                   |

Класс второго аргумента, `*genera*`, является общим, и его список приоритетов классов является

<pre>
(genera basic-os t) 
</pre>

Поскольку basic-os более специфичен, чем t, метод 1 более специфичен, чем метод 2. Используя ту же процедуру, мы обнаруживаем, что метод 3 более специфичен, чем метод 4. Таким образом, порядок приоритета методов таков (метод-1 метод-2 метод-3 метод-4)

Просто взглянув на лямбда-списки методов, вероятно, кажется правильным, что метод 1 является наиболее специфичным (потому что он специализируется на обоих параметрах), а метод 4 является наименее специфичным (потому что он не специализируется ни на одном параметре). Однако интуитивно не очевидно, какой из методов 2 и 3 является более конкретным:

|   Method  |   Lambda-list               |
|-----------|-----------------------------|
| Method 2  | ((sw basic-product) non-os) |
| Method 3  | (non-product (os basic-os)) |

CLOS оценивает метод 2 как более специфичный, чем метод 3, из-за порядка сравнения спецификаторов параметров слева направо. То есть порядок параметров в лямбда-списке влияет на приоритет методов. Поскольку специализатор крайнего левого параметра метода 2 более специфичен, чем специализатор крайнего левого параметра метода 3, метод 2 более специфичен, чем метод 3. Остальные спецификаторы параметров не рассматриваются.

Порядок приоритета параметров слева направо является произвольным по умолчанию. Вы можете изменить его, используя параметр :argument-precedence-order на defgeneric; см. "Краткое описание наследования методов", стр. 98.

### Реализация install(установки)

Обычно установка программного продукта требует нескольких отдельных шагов, которые должны выполняться в определенном порядке. Мы могли бы обнаружить, что все установки состоят из четырех отдельных этапов:

1. Восстановление программного обеспечения с ленты
2. Компиляция программной системы
3. Настройка сайта, чтобы узнать о новом программном обеспечении
4. Проверка установки программного продукта

Мы можем реализовать установку для вызова четырех обобщенных функций. Этот фреймворк позволяет удобно писать код в совместно используемых модулях.

<pre>
(defmethod install ((sw basic-product) (os basic-os)) 
   (restore-product sw os) 
   (compile-product sw os) 
   (configure-site sw os) 
   (verify-product sw os)) 
</pre>

Здесь мы рассмотрим, какие методы необходимы для каждой из четырех операций установки.

     	> Restore(Восстановить)     Это, вероятно, использует зависящую от системы функцию для восстановления данных с ленты.
	>                          Поскольку эта операция зависит только от типа операционной системы, она может быть
	>                          реализована одним основным методом для каждого типа операционной системы:

<pre>
(defmethod restore-product (sw (os genera)) body) 
(defmethod restore-product (sw (os unix)) body) 
</pre>

	>                  Аргумент sw используется для описания того, какой продукт восстанавливать с ленты.
	>                  Единственная часть restore-product, которая зависит как от типа продукта, так и от типа
	>                  операционной системы, - это путь, по которому должен храниться исходный файл. Тела
	>                  методов restore-product вызывают get-source-pathname для получения этой информации:

<pre>
(defgeneric get-source-pathname (product os) 
   (:documentation "Returns a string.")) 

(defmethod get-source-pathname ((sw life) (os unix)) 
   "/bin/games/life.lsp") 

(defmethod get-source-pathname ((sw adventure) (os unix)) 
   "/bin/games/adventure.lsp") 

(defmethod get-source-pathname ((sw life) (os genera)) 
   "sys:games;life.lisp") 

(defmethod get-source-pathname ((sw adventure) (os genera)) 
   "sys:games;adventure.lisp") 
</pre>

	> Compile(Скомпилировать)  эту операцию, вероятно, можно с помощью одного метода по умолчанию.

<pre>
(defmethod compile-product (sw os) 
(compile-file (get-source-pathname sw os))) 
</pre>

	> Configure(Настройка)       Этот шаг настраивает сайт для ознакомления с новым пакетом программного
 	>                                        обеспечения. Это может включать обновление некоторых файлов конфигурации,
	>                                        например, определение местоположения для хранения любых сообщений об
	>                                        ошибках, генерируемых программным обеспечением. Этот шаг, вероятно, зависит
	>                                        как от типа программного продукта, так и от типа операционной системы:

<pre>
(defmethod configure-site ((sw life) (os genera)) body) 
(defmethod configure-site ((sw adventure) (os genera)) bod 
(defmethod configure-site ((sw life) (os unix)) body) 
(defmethod configure-site ((sw adventure) (os unix)) body) 
</pre>

	> Verify             При этом выполняется набор тестов программного продукта, чтобы убедиться в успешности
	>                      установки. Поскольку внутренние тесты работают одинаково для данного программного
	>                      продукта независимо от типа операционной системы, методы специализируются только на
	>                      параметре программного продукта:

<pre>
(defmethod verify-product ((sw life) os) body) 
(defmethod verify-product ((sw adventure) os) body) 
</pre>

На этом этапе разработка программы завершена, и остается только реализация. Мы набросали реализацию, полностью основанную на первичных методах. Может случиться так, что этот эскиз не учитывает каждую часть инсталляции. Если мы определим поведение, которое необходимо добавить в этот эскиз, мы всегда можем использовать методы "до"(before-) и "после"(after-).

Такой дизайн обеспечивает разумный протокол. Чтобы добавить поддержку нового программного продукта или другой операционной системы, нам нужно реализовать методы только для подмножества операций установки. Такой дизайн эффективно отделяет сложности операционной системы от программного продукта:

*   Мы могли бы добавить новый программный продукт, определив методы для configure-site, verify-product и get-source-pathname.

*   Мы могли бы поддерживать новую операционную систему, определив методы для configure-site, restore-product и get-source-pathname.

## 4.4 МЕТОДЫ ДЛЯ ТИПОВ COMMON LISP

CLOS предоставляет классы, соответствующие некоторым (но не всем) стандартным типам Common Lisp. Имя такого класса совпадает с именем типа. Например, существует класс с именем array, соответствующий типу array. Эти классы предоставляются по единственной причине: чтобы вы могли писать методы, которые специализируются на типах Common Lisp.

### Классы, соответствующие типам COMMON LISP

Мы уже видели один пример класса, соответствующего типу Common Lisp — класс t, который соответствует типу t. Класс t отображается как последний (наименее специфичный) класс во всех списках приоритета классов, включая классы, определенные пользователями, и классы, предоставляемые CLOS.

В таблице 4.1 перечислены классы, соответствующие типам Common Lisp, и список приоритетов каждого класса. Эта таблица адаптирована из "Спецификации объектной системы Common Lisp" с разрешения авторов.

| Класс        | Список приоритетов классов           |
|--------------|--------------------------------------|
| array        | (array t)                            |
| bit-vector   | (bit-vector vector array sequence t) |
| character    | (character t)                        |
| complex      | (complex number t)                   |
| cons         | (cons list sequence t)               |
| float        | (float number t)                     |
| integer      | (integer rational number t)          |
| list         | (list sequence t)                    |
| null         | (null symbol list sequence t)        |
| number       | (number t)                           |
| ratio        | (ratio rational number t)            |
| rational     | (rational number t)                  |
| sequence     | (sequence t)                         |
| string       | (string vector array sequence t)     |
| symbol       | (symbol t)                           |
| t            | (t)                                  |
| vector       | (vector array sequence t)            |

Таблица 4.1 Приоритет классов, соответствующих типам Common Lisp.

Эти классы имеют множественное наследование. Отношения класса/подкласса между ними параллельны отношениям типа/подтипа, описанным в книге Стила Common LISP: The Language. Рассмотрим вектор типа:

* Тип vector - это подтип sequence(последовательности) и array(массива)
* Класс vector является подклассом sequence и array

Для каждого класса, соответствующего типу Common Lisp, если Common LISP:
Язык определяет отношение тип/подтип, соответственно определяется отношение класс/подкласс. Однако в некоторых случаях не было указано соотношение тип/подтип, и рабочая группа CLOS приняла решение о порядке приоритета. Например, строка типа является подтипом как массива, так и последовательности, но Common Lisp не определяет отношения типа/подтипа между массивом и последовательностью. Рабочая группа CLOS решила, что массив должен предшествовать последовательности в списке приоритета класса для строки,

Для каждого класса должен быть полностью определенный список приоритетов классов, чтобы CLOS могли выбирать приоритет методов. Одна из причин, по которой рабочая группа CLOS не указала, что все типы Common Lisp должны иметь соответствующие классы, заключается в том, что некоторые типы имеют неопределенные отношения с другими типами, и было бы трудно определить порядок приоритета типов. Например, рассмотрим, что произойдет, если аргументом является экземпляр 7, и применимы два метода: один специализируется на (integer 0 10), а другой специализируется на (integer 5 15) — какой метод следует считать более конкретным? Поскольку семантика некоторых типов не поддается строгому порядку приоритета, у этих типов нет соответствующих классов.

Если тип Common Lisp не имеет соответствующего класса, вы не можете определить метод, который специализируется на этом типе. CLOS не поддерживает классы для спецификаторов типов, которые являются списками, такими как

<pre>
(integer 0 10) 
(string 25) 
(not number) 
(vector (mod 256)) 
</pre>

### Встроенные классы

Обычно классы, соответствующие типам Common Lisp, реализуются как "встроенные классы", что означает, что они не обладают всеми свойствами классов, определенных с помощью defclass. Встроенные классы реализуются особым, зависящим от системы способом, чтобы использовать преимущества архитектуры машины или по другим причинам. По большей части, не имеет смысла реализовывать эти классы с помощью defclass, потому что они не совсем вписываются в эту модель.

Решение о том, как реализовать классы, соответствующие типам Common Lisp, принимается каждой реализацией CLOS. Любой из этих классов может быть реализован как встроенный класс или как класс, определяемый пользователем.
Хотя встроенные классы в некоторых отношениях следуют модели CLOS (они имеют множественное наследование; у них есть экземпляры; они наследуются от класса t, и вы можете определить методы, которые специализируются на них), они отличаются от модели другими способами. Это существенные различия между встроенными классами и пользовательскими классами:

* *Structure*.    Структура пользовательского класса представлена в виде слотов. Однако внутренняя структура встроенных классов обычно не представлена слотами. Например, значение символа, вероятно, не хранится в слоте в большинстве реализаций CLOS.
* *Creation of instances*(*Создание экземпляров*). Для создания экземпляров пользовательских классов вы можете использовать make-instance. Однако CLOS не позволяет использовать make-instance для создания экземпляров встроенных классов. Вы используете независимый механизм для создания экземпляров, например, с помощью функции cons для создания экземпляра cons.
* *Use as a superclass*(*Использовать в качестве суперкласса*). CLOS не позволяет вам включать встроенный класс в качестве прямого суперкласса любого пользовательского класса. Единственным исключением из этого правила является класс с именем t, который автоматически является суперклассом каждого класса.
* *Inheritance from* standard-object(*Наследование от* стандартного объекта). Напомним, что standard-object неявно включен как суперкласс пользовательских классов. В отличие от этого, standard-object не является суперклассом встроенных классов. Это различие позволяет реализациям предоставлять методы по умолчанию, предназначенные только для пользовательских классов.

Спецификация CLOS определяет набор требований, которым должны следовать реализации в отношении встроенных классов, но она также позволяет реализациям поддерживать расширения в этой области. Вот два расширения, с которыми вы можете столкнуться:

* Реализации CLOS могут предоставлять классы для других типов Common Lisp в дополнение к тем, которые требуются. Это может включать классы для pathname, package или других. Все такие классы должны придерживаться приоритета, подразумеваемого их отношениями типа/подтипа, определенными в Common LISP Стила: The Language.
* Как упоминалось ранее, реализации CLOS могут выбрать реализацию класса для любого типа Common Lisp либо как встроенного класса (который соответствует указанным ограничениям), либо как определяемого пользователем класса. Например, реализация CLOS может свободно определять строку класса с помощью defclass. Когда это будет сделано, вы сможете делать со строкой класса все, что вы можете делать с другими классами, определенными defclass.

Обратите внимание, что программы, которые зависят от расширений для CLOS, нелегко переносить на другие реализации CLOS.

### Примеры методов для типов COMMON LISP

Мы можем проиллюстрировать определение методов для типов Common Lisp, реализовав простой сетевой протокол для выполнения удаленного выполнения(оценки). Эта программа позволяет пользователю отправлять форму Lisp на другой компьютер по сети и получать результат вычисления(оценки) этой формы. Машина, которая отправляет форму, называется клиентской машиной, а машина, которая выполняет вычисления и возвращает результат клиенту, называется серверной машиной.

Часть этой программы, относящаяся к типам Common Lisp, заключается в отправке и получении объектов Lisp по сети. Объект Lisp живет в одном мире Lisp, и передавать фактический объект невозможно или желательно. Вместо этого клиентская машина кодирует объект Lisp в представление, подходящее для отправки по сети. С другой стороны, серверная машина декодирует представление и создает или получает объект Lisp, эквивалентный объекту, который был закодирован. Результат вычисления затем кодируется сервером, передается и декодируется клиентской машиной.

На рисунке 4.3 показано взаимодействие между клиентской и серверной машинами во время удаленного вычисления/оценки.

![Рисунок 4.3: Удаленное вычисление/оценка.](images/f04-03.jpg )**Рисунок 4.3: Удаленная вычисление/оценка.**

Декодирование и кодирование аналогичны операциям чтения и печати Lisp: Программа чтения Lisp принимает введенное представление объекта и генерирует объект Lisp; принтер принимает объект Lisp и генерирует его представление, подходящее для печати. Подобно принтеру и считывателю, методы декодирования и кодирования сохраняют только "простые" характеристики объектов. Например, содержимое массива кодируется, но любой указатель на заполнение - нет.

Реализация кодирования использует преимущества классов, соответствующих типам Common Lisp. Программа удаленного вычисления поддерживает передачу простых объектов Lisp, таких как списки, целые числа, символы, строки, символы и векторы. Мы предоставляем методы для кодирования этих объектов; эти методы специализируются на классах, соответствующих различным типам объектов. Можно расширить эту модель, чтобы отправлять экземпляры пользовательских классов, а также, предоставляя методы кодирования и декодирования для этих классов.

Хотя основной смысл этого примера состоит в том, чтобы показать кодирование и декодирование, для полноты картины мы также описываем функции верхнего уровня, которые вызывают обобщенные функции кодирования и декодирования.

### Пример Сеанса Удаленного Вычисления

Мы начнем с описания того, как использовать программу удаленного вычисления. Мы создаем remote-eval-stream и вызываем функцию remote-eval для отправки форм в поток и получения результатов. Наконец, мы закрываем поток:

<pre>
(setq *my-stream* (make-remote-eval-stream server-machine)) 
(remote-eval *my-stream* "hello") => "hello" 
(remote-eval *my-stream* '(+ 1 23)) => 24 
(close *my-stream*)
</pre>

### Функции Верхнего уровня

make-remote-eval-stream создает сетевое подключение к серверной машине и возвращает двунаправленный двоичный восьмибитный поток байтов. Как только поток создан, на сервере запускается процесс для выполнения функции eval-server (определенной ниже). Закрытие этого потока завершает серверный процесс и закрывает сетевое соединение. Поскольку реализация make-remote-eval-stream обязательно специфична для операционной системы, она здесь не приводится.

remote-eval реализует клиентскую часть; он отправляет форму на сервер, получает результат оценки этой формы и возвращает результат. Форма вычисляется в среде сервера, и любые побочные эффекты происходят в этой среде. Этот пример не передает распечатанный вывод, не передает несколько возвращаемых значений и не обрабатывает ошибки, сгенерированные при оценке формы на сервере; любая из этих функций может быть добавлена.

<pre>
;;; This must be defined on the client machine, 
(defun remote-eval (stream form) 
   (encode form stream) 
   (force-output stream) 
   ;; Read the return value 
   ;; The first byte is a control byte 
   (decode (read-byte stream) stream)) 
</pre>
eval-server реализует серверную часть; он постоянно считывает отправленные ему формы, вычисляет/оценивает формы и возвращает результаты. Когда поток закрывается, этот процесс завершается.

<pre>
;;; This must be defined on the server machine, 
(defun eval-server (stream) 
   (loop (encode 
          (eval (decode (read-byte stream) stream)) 
          stream) 
         (force-output stream)))
</pre>

### Протокол кодирования и декодирования

Основная часть программы заключается в кодировании и декодировании объектов Lisp. Для этой цели мы будем использовать простой протокол. Чтобы закодировать объект, мы сначала передаем по потоку управляющий байт, который объявляет тип объекта. (Управляющий байт - это восьмибитный байт.) Затем мы кодируем и передаем представление самого объекта; реализация этого варьируется в зависимости от типа объекта. При декодировании мы считываем управляющий байт из потока; этот байт указывает, какой тип объекта передается. Затем мы декодируем следующие байты и преобразуем закодированное представление в объект Lisp. Реализация декодирования объекта зависит от типа объекта.

Способ кодирования любого объекта одного типа (например, symbols/символов) работает вместе со способом декодирования объекта такого типа. Эти методы должны использовать одну и ту же технику для кодирования и декодирования. Однако способы кодирования и декодирования объектов другого типа (таких как символы) могут использовать совершенно иную технику кодирования и декодирования, чем техника, используемая для символов. Единственное требование заключается в том, что метод декодирования для любого данного типа объекта должен понимать, как работает метод кодирования для этого типа объекта.

Наш протокол состоит из двух обобщенных функций:

<pre>
(defgeneric encode (object stream) 
   (:documentation 
    "Encode object and send the result to stream.")) 

(defgeneric decode (code stream) 
   (:documentation 
    "Based on the code, read an encoded representation 
from the stream, decode, and create an object.")) 
</pre>

Это расширяемый протокол; его несложно расширить для поддержки дополнительных типов объектов. Для этого мы бы предоставили методы кодирования и декодирования. Нет необходимости изменять код, который вызывает encode и decode. Это одно из очевидных преимуществ использования обобщенных функций вместо того, чтобы вызывающие использовали typecase для выбора кода для кодирования и декодирования различных типов объектов.

В этом примере показано, как кодировать и декодировать несколько типов объектов. Каждый тип объекта имеет соответствующий управляющий байт. Эти управляющие байты являются

<pre>
(defconstant %positive-integer 1) 
(defconstant %negative-integer 2) 
(defconstant Icharacter 3) 
(defconstant Isymbol A) 
(defconstant Istring 5) 
(defconstant Hist 6) 
(defconstant %vector 7) 
</pre>

### Кодирование целых чисел

Чтобы закодировать целое число, мы отправляем управляющий байт, указывающий положительное или отрицательное целое число, затем длину целого числа, затем абсолютное значение самого целого числа. Длина целого числа всегда передается в одном байте.

<pre>
(defmethod encode ((num integer) stream) 
   (cond ((minusp num) 
          (write-byte %negative-integer stream) 
          (setq num (abs num))) 
         (t 
          (write-byte %positive-integer stream))) 
   (let ((n-bytes (ceiling (integer-length num) 8))) 
      (write-byte n-bytes stream) 
      (dotimes (i n-bytes) 
         (write-byte (ldb (byte 8 0) num) stream) 
         (setq num (ash num -8)))))
</pre>

Чтобы декодировать целые числа, нам понадобится один метод для декодирования положительных целых чисел и другой метод для декодирования отрицательных целых чисел. Метод, который декодирует положительные целые числа, будет вызван, когда код равен %positive-integer. Этот метод считает длину целого числа, а затем само целое число; затем он может создать (прочитать) целое число и завершить работу. Следующий байт в потоке - это новый управляющий байт, который является началом нового закодированного объекта.

Отправляя длину целых чисел в одном байте, мы неявно наложили ограничение на передаваемое значение целых чисел: не может быть передано значение num, для которого (> (ceiling (integer-length (abs num)) 8) 255) равно true, что означает, что максимальная длина целого числа равна 2040. Используя определение целочисленной длины на странице 224 книги Стила Common LISP: The Language, максимальное положительное целое число равно (1- (expt 2 (* 8 255))), а самое отрицательное целое число является отрицательным от этого. Таким образом, предел очень велик, чуть более 10, увеличенный до 614-й степени.

Методы декодирования приведены в следующем разделе, "Методы для отдельных объектов LISP", стр. 94.

### Кодировка Characters(символьных знаков)

Чтобы закодировать символьный знак, мы отправляем управляющий байт, указывающий что это символьный знак, а затем ASCII-код символа в следующем байте. Этот метод отправляет только код символьного знака и игнорирует любой шрифт или биты.

Чтобы учесть различия в собственных наборах символьных знаков серверной и клиентской машин, мы преобразуем символьный знак в его код ASCII при кодировании и преобразуем его из ASCII в соответствующий код в собственном наборе символьных знаков при декодировании. Функция char-to-ascii зависит от реализации и здесь не предоставляется. Необходимо, чтобы charto-ascii возвращал значение меньше 256, чтобы оно помещалось в восьмибитный байт.

<pre>
(defmethod encode ((char character) stream) 
   (write-byte %character stream) 
   (write-byte (char-to-ascii char) stream)) 
</pre>

### Кодирование списков и векторов

Чтобы закодировать список или вектор, мы определяем соответствующий управляющий байт, затем вызываем encode-sequence для отправки управляющего байта, длины последовательности и кодированного представления каждого элемента последовательности.

<pre>
(defmethod encode ((list list) stream) 
   (encode-sequence list stream %list)) 

(defmethod encode ((vector vector) stream) 
   (encode-sequence vector stream %vector)) 

(defun encode-sequence (seq stream code) 
   (let ((length (length seq))) 
      (write-byte code stream) 
      (encode length stream) 
      (dotimes (i length) 
         (encode (elt seq i) stream)))) 
</pre>

Эти два метода вызывают функцию encode-sequence для выполнения разделяемой части работы. Это модульная конструкция, которая использует обычную функцию вместо обобщенной функции. В этом контексте нет никакого преимущества в реализации encode-sequence как обобщенные функции.

### Кодирование строк

Строка - это вектор, поэтому метод кодирования векторов применим к строкам, и он будет отлично работать для них. Однако мы собираемся предоставить метод для string по соображениям эффективности. Легко оптимизировать кодировку строк, потому что мы знаем, что каждый элемент строки является символьным знаком и умещается в одном байте(для ASCII символов, но не UTF8). Когда мы кодируем строку, нет необходимости кодировать каждый символ строки. Напротив, нет никакого способа узнать, каковы элементы вектора, поэтому каждый элемент должен быть закодирован.

Чтобы закодировать строку, мы отправляем управляющий байт, указывающий строку, затем длину строки, затем каждый символ строки. Любые указатели заливки, биты или шрифты не кодируются.

<pre>
(defmethod encode ((string string) stream) 
   (let ((length (length string))) 
      (write-byte %string stream) 
      ;; careful to allow strings greater than 256 chars 
      (encode length stream) 
      (dotimes (i length) 
         (write-byte (char-to-ascii (aref string i)) stream))))
</pre>

Когда encode вызывается со строкой в качестве первого аргумента, применяются два метода: метод, который специализируется на string, и метод, который специализируется на vector. Чтобы определить, какой метод является более конкретным, CLOS обращается к списку приоритетов класса string, который является

<pre>
(string vector array sequence t) 
</pre>

Поскольку класс string имеет приоритет над классом vector, метод, который специализируется на string, более специфичен, чем метод, который специализируется на vector.

### Кодирование Символов

Чтобы закодировать символ и его пакет, мы отправляем управляющий байт, указывающий на  символ, закодированное представление имени символа и закодированное представление пакета символа. Обратите внимание, что значение, определение функции и любые свойства символа не передаются. Кроме того, пакет должен существовать на стороне сервера.

<pre>
(defmethod encode ((symbol symbol) stream) 
   (write-byte %symbol stream) 
   (encode (symbol-name symbol) stream) 
   (encode (package-name (symbol-package symbol)) stream)) 
</pre>

### Соображения по проектированию и эффективности

Обратите внимание, что метод для символьного знака довольно эффективен; он отправляет один управляющий байт, затем код символа ASCII. Напротив, метод для символа приводит к большим накладным расходам. Этот метод отправляет управляющий байт, указывающий символ, затем закодированное представление имени символа, затем закодированное представление пакета символа. В таблице 4.2 показано кодированное представление символа + в пакете lisp.

| Datum   | Meaning                   |
|---------|---------------------------|
| 4       | %symbol                   |
| 5       | %string                   |
| 1       | %positive-integer         |
| 1       | length of the integer     |
| 1       | length of the string      |
| 43      | ASCII character code of + |
| 5       | %string                   |
| 1       | %positive-integer         |
| 1       | length of the integer     |
| 4       | length of the string      |
| 76      | ASCII character code of L |
| 73      | ASCII character code of I |
| 83      | ASCII character code of S |
| 80      | ASCII character code of P |

Таблица 4.2 Кодировка символа + в пакете lisp.

Закодированное представление символов использует пять управляющих байтов. Это результат использования encode как для имени символа, так и для его упаковки. Если важна скорость, мы можем разработать более эффективную стратегию кодирования символов. Однако нынешняя стратегия имеет два преимущества. Во-первых, она позволяет проверять ошибки на стороне декодирования: метод декодирования ожидает, что символ будет содержать строку (имя символа), за которой следует другая строка (пакет символа). Во-вторых, этот метод вызывает метод, который специализируется на string, вместо того, чтобы дублировать работу, выполняемую этим методом.

Общая конструкция кодирования и декодирования проста, модульна и эффективна. Этот дизайн легко вмещает улучшения и дополнения к программе в нескольких областях:

* Настройка для повышения эффективности
* Передача нескольких возвращаемых значений
* Передача печатного вывода
* Обработка ошибок, возникших при вычислении формы на сервере
* Расширение этого протокола для передачи дополнительных типов объектов

## 4.5 МЕТОДЫ ДЛЯ ОТДЕЛЬНЫХ ОБЪЕКТОВ LISP

До сих пор мы видели методы, которые специализируются на классе аргументов обообщенной функции. То есть специализаторы параметров - это классы. Иногда бывает полезно написать метод, который специализируется на отдельном объекте Lisp. Метод, который специализирует один из своих параметров на отдельном объекте Lisp, называется индивидуальным(individual) методом.

### Применимость и приоритет Индивидуальных методов

Предположим, у вас есть операция, которая работает определенным образом, когда аргументом является число, но должна работать по-другому, если аргументом является целое число 0. Метод 2 - это индивидуальный метод. Его второй параметр специализирован на целом числе 0. Специализатором параметра является список (eql 0).

<pre>
;;; Method 1 
(defmethod divide ((dividend number) (divisor number)) 
   (/ dividend divisor)) 
;;; Method 2 
(defmethod divide ((dividend number) (zero (eql 0))) 
   (error "Cannot divide by zero.")) 
</pre>
Лямбда-список отдельного метода содержит такой параметр, как

<pre>
(var (eql form)) 
</pre>

Специализатором параметра является (eql object), где object - это результат вычисления формы. Форма вычисляется только один раз, когда определен метод.

Когда divide вызывается с 0 в качестве второго аргумента, применимы оба метода. Далее следует правило применимости отдельных методов. Обратите внимание, что, поскольку в тесте на применимость используется eql, метод 2 применим только для целого числа 0, а не для 0.0.

| Правило применимости индивидуального метода:                               |
|----------------------------------------------------------------------------|
| Для специализатора параметров формы (объекта eql) аргумент обобщенной      |
| функции удовлетворяет специализатору параметров, если аргумент является    |
| eql для объекта. Другими словами, это выражение должно быть истинным:      |
| (объект аргумента eql ')                                                   |

Метод 2 более специфичен, как и следовало ожидать. Следует правило приоритета отдельных методов.

| Правило приоритета отдельного метода:                                      |
|----------------------------------------------------------------------------|
| Когда сравниваются два специализатора параметров, параметр,                |
| специализированный в форме (объект eql), всегда более специфичен, чем      |
| класс.                                                                     |

### Примеры индивидуальных методов

Рассмотрим задачу декодирования закодированных представлений объектов Lisp. Каждый объект Lisp кодируется и декодируется в соответствии со своим типом, поэтому имеет смысл писать отдельные методы для кодирования и декодирования каждого отдельного типа объекта.

Кодировщик имеет объект Lisp в качестве своего аргумента, поэтому эти методы могут специализироваться на классах объектов. Однако декодер не имеет объектов Lisp в качестве аргументов; вместо этого он считывает данные из потока и получает предупреждение о типе объекта с помощью управляющего байта, предшествующего объекту в потоке. Таким образом, декодер не может предоставлять методы, которые специализируются на классах, но он может использовать отдельные методы, которые специализируются на каждом из этих управляющих байтов.

Здесь повторяется декодирование обобщенной функции:

<pre>
(defgeneric decode (code stream) 
   (:documentation 
    "Based on the code, read an encoded representation 
from the stream, decode, and create an object.")) 
</pre>

Каждый метод для decode(декодирования) специфицирует свой первый аргумент, и ровно один метод применим для каждого отдельного управляющего байта. Напомним, что decode вызывается с одним байтом в качестве первого аргумента (это управляющий байт) и потоком в качестве второго аргумента. Определение remote-eval повторяется здесь, чтобы проиллюстрировать, как вызывается decode:

<pre>
(defun remote-eval (stream form) 
   (encode form stream) 
   (force-output stream) 
   ;; Read the return value 
   ;; The first byte is a control byte 
   (decode (read-byte stream) stream)) 
</pre>

### Декодирование целых чисел

Нам нужны два метода для декодирования целых чисел, потому что метод кодирования целых чисел отправляет два разных управляющих байта, в зависимости от того, является ли целое число положительным или отрицательным. Первый метод применим, когда первым аргументом является управляющий байт, указывающий на положительное целое число. Аналогично, второй метод применим, когда первым аргументом является управляющий байт, указывающий на отрицательное целое число.

<pre>
(defmethod decode ((code (eql Ipositive-integer)) stream) 
   (decode-integer stream)) 

(defmethod decode ((code (eql %negative-integer)) stream) 
   (- (decode-integer stream))) 

(defun decode-integer (stream) 
   (let ((num 0) 
         (n-bytes (read-byte stream))) 
      (dotimes (i n-bytes) 
         (setq num (dpb (read-byte stream) (byte 8 (* i 8)) num))) 
      num)) 
</pre>

%positive-integer и %negative-integer вычисляются ровно один раз, в момент определения этих методов.

Функция decode-integer выполняет разделяемую часть работы; она вызывается обоими методами. Нет никакого преимущества в реализации decode-integer в качестве обобщенной функции.

### Декодирование символьных знаков(Characters)

Чтобы декодировать символьный знак, мы считываем один байт и преобразуем его из кода ASCII в соответствующий код символьного знака в нативном наборе символьных знаков. Функция ascii-to-char зависит от реализации и здесь не предусмотрена.

<pre>
(defmethod decode ((code (eql %character)) stream) 
   (ascii-to-char (read-byte stream))) 
</pre>

### Декодирование списков и векторов

Чтобы декодировать список или вектор, мы считываем длину последовательности, затем декодируем каждый элемент в последовательности. Опять же, мы можем использовать функцию для выполнения разделяемой части работы.

<pre>
(defmethod decode ((code (eql %list)) stream) 
   (decode-sequence stream 'list)) 

(defmethod decode ((code (eql %vector)) stream) 
   (decode-sequence stream 'vector)) 

(defun decode-sequence (stream type) 
   (let* ((length (decode (read-byte stream) stream)) 
          (seq (make-sequence type length))) 
      (dotimes (i length) 
         (setf (elt seq i) 
               (decode (read-byte stream) stream))) 
      seq)) 
</pre>

### Декодирование строк

Чтобы декодировать строку, мы считываем длину строки, затем каждый символьный знак строки.

<pre>
(defmethod decode ((code (eql Istring)) stream) 
   (let* ((length (decode (read-byte stream) stream)) 
          (string (make-string length))) 
      (dotimes (i length) 
         (setf (aref string i) (ascii-to-char (read-byte stream)))) 
      string)) 
</pre>

### Декодирование символов

Чтобы декодировать символьный знак, мы сначала расшифровываем название, а затем расшифровываем пакет. Обратите внимание, что этот метод не создает пакет, если он еще не существует.

<pre>
;;; Does not create the package. 
(defmethod decode ((code (eql %symbol)) stream) 
   (let ((code (read-byte stream))) 
      (when (/- %string code) 
         (error "The symbol's name must be a string.")) 
      (let ((symbol-name (decode code stream))) 
         (setq code (read-byte stream)) 
         (when (/- %string code) 
            (error "The symbol's package name must be a string.")) 
         (let* ((pkg-name (decode code stream)) 
                (pkg (find-package pkg-name))) 
            (when (null pkg) 
               (error "Package named ~A not found." pkg-name)) 
            (intern symbol-name pkg))))) 
</pre>

## 4.6 КРАТКОЕ ОПИСАНИЕ НАСЛЕДОВАНИЯ МЕТОДОВ

Наследование методов включает в себя два отдельных механизма. И то, и другое происходит автоматически как часть generic dispatch(обобщенной диспетчеризации).

1. Выбор набора применимых методов2. Ранжирование применимых методов в порядке приоритета

Чтобы выбрать набор применимых методов, CLOS требует знания аргументов обобщенной функции и методов, определенных для обобщенной функции. Следующее общее правило учитывает все виды методов, включая методы, которые специализируются только на одном параметре, мультиметоды и отдельные методы:

| Общее правило применимости метода:                                                            |
|-------------------------------------------------------------------------------------------------------------------------|
| Метод применим, если каждому из его требуемых параметров удовлетворяет |
| соответствующий аргумент (arg) обобщенной функции.             |


| Требуемый параметр  | Тест                 |
|-----------------------------------|-------------------------|
| (var (eql form))             |  (eql arg 'object) |
| (var class-name)          |  (typep arg 'class-name)|
|  var                             |  (typep arg 't)    |

В первом случае объект получается путем оценки формы один раз, в момент определения метода. В последнем случае требуемый параметр является неспециализированным, что эквивалентно специализации на классе t. Любой аргумент удовлетворяет неспециализированному параметру, поскольку каждый объект Lisp имеет тип t.

Чтобы ранжировать применимые методы в порядке приоритета, CLOS требует знания следующего:

* Набор применимых методов
* Список приоритета класса каждого требуемого аргумента обобщенной функции
* Порядок приоритета аргументов обобщенной функции

При ранжировании приоритета двух методов CLOS сравнивает специализаторы параметров методов. Неспециализированный параметр эквивалентен классу с именем t, являющемуся специализатором параметра.

Обычно порядок приоритета аргументов слева направо, что означает, что CLOS начинается со сравнения первого (крайнего левого) специализатора параметров метода A с первым специализатором параметров метода B. Если два специализатора параметров различны, CLOS использует следующее правило, чтобы определить, какой специализатор параметров более специфичен, и ранжирует методы на этой основе, не принимая во внимание какие-либо другие специализаторы параметров. Однако, если два специализатора параметров одинаковы, CLOS не может ранжировать приоритет методов на этой основе. CLOS переходит к следующей паре специализаторов параметров и так далее, пока не найдет пару специализаторов параметров, которые отличаются; затем методы ранжируются на основе этой пары специализаторов параметров.

Если два метода имеют одинаковые спецификаторы параметров, они должны иметь разные квалификаторы. В этом случае не имеет значения, какой метод является более конкретным.

| Правило ранжирования специализаторов параметров:                       |
|------------------------------------------------------------------------|
| Специализатор параметров (eql object) более специфичен, чем            |
| класс.                                                                 |

Когда оба специализатора параметров являются классами, они ранжируются в соответствии со списком приоритетов классов класса соответствующего аргумента универсальной функции. Список приоритетов классов указывает, какой класс более специфичен, чем другой.

### Порядок приоритета Аргументов

Приоритет методов зависит от порядка приоритета аргументов обобщенной функции. По умолчанию CLOS использует порядок приоритета аргументов слева направо, что означает, что первый аргумент (самый левый) более важен для ранжирования приоритета, чем следующие аргументы. Вы можете использовать параметр :argument-precedence-order для defgeneric, чтобы указать другой порядок.

Порядок приоритета аргументов важен, когда CLOS ранжирует два метода, которые специализируются на разных параметрах, или методы, которые специализируются более чем на одном параметре. Рассмотрим следующие методы:

<pre>
;;; Method A 
(defmethod install ((sw basic-product) non-os) 
   body)

;;; Method B 
(defmethod install (non-product (os basic-os)) 
   body) 
</pre>

Метод A специализируется на своем первом параметре, но оставляет свой второй параметр неспециализированным. Метод B специализирует свой второй параметр, но оставляет свой первый параметр неспециализированным. Если мы рассмотрим первые параметры изолированно, метод A более специфичен, поскольку любой специализированный параметр имеет приоритет над неспециализированным. Однако, если мы рассмотрим вторые параметры изолированно, метод B более специфичен. Порядок приоритета аргументов разрешает этот конфликт, указывая, какой из аргументов следует рассмотреть первым.